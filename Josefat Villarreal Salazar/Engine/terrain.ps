////////////////////////////////////////////////////////////////////////////////
// Filename: terrain.ps
////////////////////////////////////////////////////////////////////////////////


/////////////
// GLOBALS //
/////////////
Texture2D shaderTexture[5];
SamplerState SampleType;

cbuffer LightBuffer
{
	float4 ambientColor;
	float4 diffuseColor;
    float3 lightDirection;
	float padding;
};


//////////////
// TYPEDEFS //
//////////////
struct PixelInputType
{
    float4 position : SV_POSITION;
	float2 tex : TEXCOORD0;
	float2 tex2 : TEXCOORD1;
	float3 normal : NORMAL;
	float4 worldPosition : TEXCOORD2;
};


////////////////////////////////////////////////////////////////////////////////
// Pixel Shader
////////////////////////////////////////////////////////////////////////////////
float4 TerrainPixelShader(PixelInputType input) : SV_TARGET
{
	float4 textureColor;
	float3 lightDir;
	float lightIntensity;
	float4 color;

	// Sample the pixel color from the texture using the sampler at this texture coordinate location.
    float4 grassTexture = shaderTexture[0].Sample(SampleType, input.tex);
	float4 soilTexture = shaderTexture[1].Sample(SampleType, input.tex);
	float4 rockTexture = shaderTexture[2].Sample(SampleType, input.tex);
	float4 snowTexture = shaderTexture[3].Sample(SampleType, input.tex);
	float4 alphaTexture = shaderTexture[4].Sample(SampleType, input.tex2);

	textureColor = lerp(soilTexture, grassTexture, alphaTexture);
	textureColor = lerp(textureColor, snowTexture, input.worldPosition.y / (255.0 / 15.0) );
	textureColor = lerp(rockTexture, textureColor, input.normal.y);

	// Set the default output color to the ambient light value for all pixels.
    color = ambientColor;

	// Invert the light direction for calculations.
    lightDir = -lightDirection;

    // Calculate the amount of light on this pixel.
    lightIntensity = saturate(dot(input.normal, lightDir));

	if(lightIntensity > 0.0f)
    {
        // Determine the final diffuse color based on the diffuse color and the amount of light intensity.
        color += (diffuseColor * lightIntensity);
    }

    // Saturate the final light color.
    color = saturate(color);

	// Multiply the texture pixel and the final light color to get the result.
    color = color * textureColor;

    return color;
}